buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-beta05'
        classpath Plugins.kotlin
        classpath Plugins.navigation_safeargs
        classpath Plugins.google_services_gradle
        classpath Plugins.versions_gradle
        classpath Plugins.firebaseCrashlyticsPlugin
        classpath Plugins.firebasePerformancePlugin
        classpath Plugins.daggerHiltPlugin
        classpath Plugins.aboutLibrariesPlugin
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.5'

        classpath "com.github.triplet.gradle:play-publisher:3.5.0-agp7.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply from: rootProject.file('gradle/versioning.gradle')

allprojects {
    group "de.psdev.devdrawer"
    version enhancedVersion(2, 1, 0)

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    project.ext {
        // Project versions
        gitHash = gitScmVersion()
        appVersionName = project.version
        appVersionCode = buildVersionCode()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion = '7.1.1'
    distributionType = "all"
}

task printVersion() {
    println("Version: ${project.version}")
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}